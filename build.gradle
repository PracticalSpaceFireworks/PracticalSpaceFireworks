plugins {
    // Cannot use ForgeGradle 3+ as it does not support access transformers
    id 'net.minecraftforge.gradle.forge' version '2.3.4'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'net.nemerosa.versioning' version '2.15.1'
}

ext.friendlyName = name.replaceAll(/(?!^)([A-Z])(?![A-Z])/, ' $1')
ext.buildNumber = System.getenv('BUILD_NUMBER') ?: versioning.info.build

group = "net.gegy1000"
version = "0.2.1-$buildNumber"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

lombok {
    version = '1.18.22'
    sha256 = '39f3922deb679b1852af519eb227157ef2dd0a21eec3542c8ce1b45f2df39742'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    mappings = 'stable_39'
    runDir = 'minecraft'
}

repositories {
    maven {
        url = 'https://maven.tterrag.com'
    }

    maven {
        url = 'https://cursemaven.com'
    }

    maven {
        url = 'https://dvs1.progwml6.com/files/maven'
    }
}

dependencies {
    implementation 'team.chisel.ctm:CTM:MC1.12.2-1.0.2.31'
    implementation 'curse.maven:hwyla-253449:2568751' // 1.8.26-B41_1.12.2
    implementation 'mezz.jei:jei_1.12.2:4.16.1.302'
}

compileJava {
    options.with {
        it.fork = true
        it.deprecation = true
        it.encoding = 'UTF-8'
        it.compilerArgs.addAll(['-Xlint:all', '-parameters'])
    }
}

processResources {
    filesMatching(['/mcmod.info', 'version.properties']) {
        expand 'version': version
    }
}

jar {
    from '/LICENSE'

    from sourceSets.api.output
    from sourceSets.main.output
}

task apiJar(type: Jar) {
    from '/LICENSE'

    from sourceSets.api.allSource
    from sourceSets.api.output

    classifier = 'api'
}

sourceJar {
    from '/LICENSE'
}

reobf {
    apiJar {
        mappingType = 'SEARGE'
    }
}

assemble {
    dependsOn apiJar, reobfApiJar
}

tasks.eclipse {
    dependsOn installLombok
}

if (project.hasProperty('curseforge_key')) {
    curseforge {
        apiKey = project.curseforge_key

        project {
            id = '291502'
            releaseType = 'beta'
            addGameVersion '1.12.2'

            changelogType = 'html'
            changelog = System.getenv('CHANGELOG')

            if (changelog == null || changelog == 'none') {
                changelog = parseChangelog()
            }

            mainArtifact(jar) {
                displayName = "$friendlyName - $version"
            }

            relations {
                requiredDependency 'ctm'
            }
        }
    }
}

String parseChangelog() {
    def changelog = "<h2>$friendlyName $version</h2>"

    file('changelog.txt').withReader('UTF-8') { reader ->
        reader.readLine() // Skip version header

        String line, lastLine = ''

        while ((line = reader.readLine()) != null) {
            if (line.startsWith('- ')) {
                changelog += '<li>'

                def matcher = line =~ /-\s#(\d+)\s(.+)$/

                if (matcher.matches()) {
                    def id = matcher.group(1)
                    def description = matcher.group(2)

                    changelog += "<a href='https://github.com/$name/$name/issues/$id' target='_blank'>#$id</a>"
                    changelog += " $description"

                } else {
                    changelog += line.substring('- '.length())
                }

                changelog += '</li>'
            } else {
                if (!lastLine.empty) {
                    changelog += '</ul>'
                }

                if (line.empty) {
                    break
                }

                changelog += "<h4>$line</h4>"
                changelog += '<ul>'
                lastLine = line
            }
        }
    }

    return changelog
}
